Libiberty needs config.h to do the right thing, or we get warnings
about prototypes not being used.

diff -drupP --exclude-from=/M/donn/diffs/exclude.files gas.prev/acinclude.m4 gas/acinclude.m4
--- gas.prev/acinclude.m4	Sun Feb 10 10:05:41 2002
+++ gas/acinclude.m4	Sun Feb 10 19:16:39 2002
@@ -70,3 +70,60 @@ ifelse(yes,no,[
 AC_DEFUN([CY_WITH_NLS],)
 AC_SUBST(INTLLIBS)
 ])
+
+dnl The following are taken from gcc (thus their name) so that when
+dnl compiling libiberty.h and libiberty/getopt.h the right things happen.
+
+dnl See whether we need a declaration for a function.
+dnl The result is highly dependent on the INCLUDES passed in, so make sure
+dnl to use a different cache variable name in this macro if it is invoked
+dnl in a different context somewhere else.
+dnl gcc_AC_CHECK_DECL(SYMBOL,
+dnl 	[ACTION-IF-FOUND [, ACTION-IF-NOT-FOUND [, INCLUDES]]])
+AC_DEFUN(gcc_AC_CHECK_DECL,
+[AC_MSG_CHECKING([whether $1 is declared])
+AC_CACHE_VAL(gcc_cv_have_decl_$1,
+[AC_TRY_COMPILE([$4],
+[#ifndef $1
+char *(*pfn) = (char *(*)) $1 ;
+#endif], eval "gcc_cv_have_decl_$1=yes", eval "gcc_cv_have_decl_$1=no")])
+if eval "test \"`echo '$gcc_cv_have_decl_'$1`\" = yes"; then
+  AC_MSG_RESULT(yes) ; ifelse([$2], , :, [$2])
+else
+  AC_MSG_RESULT(no) ; ifelse([$3], , :, [$3])
+fi
+])
+
+dnl
+dnl Check multiple functions to see whether each needs a declaration.
+dnl Arrange to define HAVE_DECL_<FUNCTION> to 0 or 1 as appropriate.
+dnl gcc_AC_CHECK_DECLS(SYMBOLS,
+dnl 	[ACTION-IF-NEEDED [, ACTION-IF-NOT-NEEDED [, INCLUDES]]])
+AC_DEFUN(gcc_AC_CHECK_DECLS,
+[for ac_func in $1
+do
+changequote(, )dnl
+  ac_tr_decl=HAVE_DECL_`echo $ac_func | tr 'abcdefghijklmnopqrstuvwxyz' 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'`
+changequote([, ])dnl
+gcc_AC_CHECK_DECL($ac_func,
+  [AC_DEFINE_UNQUOTED($ac_tr_decl, 1) $2],
+  [AC_DEFINE_UNQUOTED($ac_tr_decl, 0) $3],
+dnl It is possible that the include files passed in here are local headers
+dnl which supply a backup declaration for the relevant prototype based on
+dnl the definition of (or lack of) the HAVE_DECL_ macro.  If so, this test
+dnl will always return success.  E.g. see libiberty.h's handling of
+dnl `basename'.  To avoid this, we define the relevant HAVE_DECL_ macro to
+dnl 1 so that any local headers used do not provide their own prototype
+dnl during this test.
+#undef $ac_tr_decl
+#define $ac_tr_decl 1
+  $4
+)
+done
+dnl Automatically generate config.h entries via autoheader.
+if test x = y ; then
+  patsubst(translit([$1], [a-z], [A-Z]), [\w+],
+    [AC_DEFINE([HAVE_DECL_\&], 1,
+      [Define to 1 if we found this declaration otherwise define to 0.])])dnl
+fi
+])
diff -drupP --exclude-from=/M/donn/diffs/exclude.files gas.prev/config.in gas/config.in
--- gas.prev/config.in	Sun Feb 10 10:05:41 2002
+++ gas/config.in	Sun Feb 10 19:38:54 2002
@@ -274,3 +274,9 @@
 /* Define if errno is not declared in system header files. */
 #undef NEED_DECLARATION_ERRNO
 
+/* Define to 1 if we found this declaration otherwise define to 0. */
+#undef HAVE_DECL_BASENAME
+
+/* Define to 1 if we found this declaration otherwise define to 0. */
+#undef HAVE_DECL_GETOPT
+
diff -drupP --exclude-from=/M/donn/diffs/exclude.files gas.prev/configure.in gas/configure.in
--- gas.prev/configure.in	Sun Feb 10 10:05:42 2002
+++ gas/configure.in	Sun Feb 10 19:41:59 2002
@@ -899,6 +899,12 @@ dnl AC_CONST
 AC_FUNC_ALLOCA
 AC_C_INLINE
 
+# We will need to find libiberty.h and ansidecl.h
+saved_CFLAGS="$CFLAGS"
+CFLAGS="$CFLAGS -I${srcdir} -I${srcdir}/../include"
+gcc_AC_CHECK_DECLS(basename getopt, , ,[
+#include "unistd.h"])
+
 # VMS doesn't have unlink.
 AC_CHECK_FUNCS(unlink remove, break)
 
